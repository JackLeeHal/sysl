CodeGenTransform:
  !view IsKeyword(word <: string) -> bool:
    word -> (:
      out = word in {"break", "default","func","interface","select","case","defer","go","map","struct","chan","else","goto","package","switch","const","fallthrough","if","range","type","continue","for","import","return","var"}
    )

  !view GoType(t <: sysl.Type) -> string:
    t -> (:
      out = if t.type ==:
        "primitive" => if t.primitive ==:
          "DECIMAL" => "double"
          "INT" => "int64"
          "FLOAT" => "float64"
          "STRING" => "string"
          "STRING_8" => "string"
          "BOOL" => "bool"
        "sequence" => "[]" + GoType(t.sequence).out
        else t.type_ref
    )

  !view GetGoField(word <: string) -> string:
    word -> (:
      let wordUpper = ToTitle(word)
      # Common initialisms https://github.com/golang/lint/blob/8f45f776aaf18cebc8d65861cc70c33c60471952/lint.go#L771
      out = if (wordUpper in {"ACL", "API", "ASCII", "CPU", "CSS", "DNS", "EOF", "GUID", "HTML", "HTTP", "HTTPS", "ID", "IP", "JSON", "LHS", "QPS", "RAM", "RHS", "RPC", "SLA", "SMTP", "SQL", "SSH", "TCP", "TLS", "TTL", "UDP", "UI", "UID", "UUID", "URI", "URL", "UTF8", "VM", "XML", "XMPP", "XSRF", "XSS"}) == true then wordUpper else Title(ToLower(word))
    )

  !view filename(app <: sysl.App) -> string:
    app -> (:
      filename = app.name + ".go"
    )

  !view GoStructFields(fields <: set of Type) -> set of FieldDecl:
    fields -> (field:
      let fieldName = GetGoField(field.key).out
      identifier = fieldName
      let IsPtr = if field.value.optional == true then "*" else ""
      Type = IsPtr + GoType(field.value).out
      let jsonPrefix = '`json:"'
      let fieldTag = if field.value.attrs.json_tag == null then (jsonPrefix + ToLower(fieldName)) else (jsonPrefix + field.value.attrs.json_tag)
      Tag = if field.value.optional == true then (fieldTag + ',omitempty"`') else (fieldTag + '"`')
    )

  !view GoStructs(types <: set of Type) -> set of TopLevelDecl:
    types -> (type:
        Declaration = type -> <Declaration>(:
          StructType = type -> <StructType>(:
            Comment = "\n//" + .key + " ..." 
            StructName = .key
            FieldDecl = GoStructFields(.value.fields)
          )
        )
    )

  !view GoInterfaces(unions <: set of Type) -> set of TopLevelDecl:
    unions -> (union:
        Declaration = union -> <Declaration>(:
          InterfaceType = union -> <InterfaceType>(:
            InterfaceName = .key
            MethodSpec = {"fmt.Formatter", "GoIsExpr" } -> <MethodSpec> (name:
              MethodName = name
            )
            let methodName = "GoIs" + .key
            MethodDecl = union.value.fields -> <set of MethodDecl> (receiverName:
              Receiver = receiverName -> <Receiver>(:
                ReceiverType = "*" + receiverName
              )
              FunctionName = methodName
              Signature = receiverName -> <Signature> (:
                Parameters = {""} -> <Parameters> (t:
                  ParameterList = t
                )
              )
              Block = receiverName -> <Block> (:
                out = ""
              )
            )
          )
        )
    )

  !view goFile(app <: sysl.App) -> string:
    app -> (:

      PackageClause = .name -> <package> (name:
        PackageName = ToLower(name)
      )

      # ImportDecl = app -> <ImportDecl>(:
      #   ImportSpec = {"package1", "package2"} -> <set of ImportSpec> (name:
      #     Import = name
      #   )
      # )

      TopLevelDecl = GoStructs(app.types) | GoInterfaces(app.union)
    )
